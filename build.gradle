version '1.0'

apply plugin: 'maven'
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'

sourceCompatibility = 1.8
targetCompatibility = 1.8

[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

// In this section you declare where to find the dependencies of your project
repositories {
    maven { url "http://maven.aliyun.com/nexus/content/groups/public/"}
    maven { url 'https://dl.bintray.com/ethereum/maven/'}
    mavenLocal()
    mavenCentral()
}


def spring_boot_version="1.5.9.RELEASE"
List spring_boot =[ 
    "org.springframework.boot:spring-boot-starter-web:$spring_boot_version",
    "org.springframework.boot:spring-boot-autoconfigure:$spring_boot_version",
    "org.springframework.boot:spring-boot-configuration-processor:$spring_boot_version",
    "org.springframework.boot:spring-boot-starter-log4j2:$spring_boot_version"
]

List mysql = [
    'mysql:mysql-connector-java:8.0.13',
    'org.mybatis:mybatis:3.2.8',
    'org.mybatis.spring.boot:mybatis-spring-boot-starter:1.3.1'
]

List swagger = [
    'io.springfox:springfox-swagger2:2.8.0',
    'io.springfox:springfox-swagger-ui:2.8.0'
]

def log4j_version="2.9.0"
List logger = [
    "org.apache.logging.log4j:log4j-api:$log4j_version",
    "org.apache.logging.log4j:log4j-core:$log4j_version",
    "org.apache.logging.log4j:log4j-slf4j-impl:$log4j_version",
    "org.apache.logging.log4j:log4j-web:$log4j_version",
    "org.slf4j:jcl-over-slf4j:1.7.12"
]

List netty = [
    'io.netty:netty-all:4.1.15.Final',
    'io.netty:netty-tcnative:2.0.0.Final'
]

List dangdang = [
    'com.dangdang:elastic-job-lite-core:2.1.5',
    'com.dangdang:elastic-job-lite-spring:2.1.5'
]

List zookeeper = [
    'org.apache.zookeeper:zookeeper:3.4.10',
    'com.github.sgroschupf:zkclient:0.1'
]

List web3sdk = [
//    'org.ethereum:solcJ-all:0.4.25', // web3sdk已包含该包 无需再次引入
    'org.apache.httpcomponents:httpclient:4.5.2',
    'org.bouncycastle:bcprov-jdk15on:1.54',
    'com.lambdaworks:scrypt:1.4.0',
    'com.squareup:javapoet:1.7.0',
    'io.reactivex.rxjava2:rxjava:2.2.7',
    'com.github.jnr:jnr-unixsocket:0.15'
]

List jaxb = [
     "javax.xml.bind:jaxb-api:2.3.0",
     "com.sun.xml.bind:jaxb-impl:2.3.0",
     "com.sun.xml.bind:jaxb-core:2.3.0",
     "javax.activation:activation:1.1.1"
]

dependencies {
    compile spring_boot,mysql,swagger,web3sdk,logger,netty,dangdang,zookeeper,jaxb
    // guomi contract compile: USE 'socJ-all-0.4.25-gm' to REPLACE 'ethereum:solcJ-all'
    compile ("org.fisco-bcos:web3sdk:2.2.0")
//        {
//            exclude group:"org.ethereum"
//        }
//    compile fileTree(dir:'lib',includes:['solcJ-all-0.4.25-gm.jar'])
    compile "org.apache.commons:commons-lang3:3.6"
    compile "commons-io:commons-io:2.4"
    compile "commons-fileupload:commons-fileupload:1.4"
    compile "com.alibaba:fastjson:1.2.60"
    compile "io.shardingsphere:sharding-jdbc-spring-boot-starter:3.1.0"
    compile 'org.projectlombok:lombok:1.18.2'
    annotationProcessor 'org.projectlombok:lombok:1.18.2'
}

configurations {
    all*.exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    all*.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    all*.exclude group: 'log4j', module: 'log4j'
    all*.exclude group: 'com.mchange', module: '*'
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
        resources  {
            srcDir 'src/main/resources'
        }
    }
}

clean {
    delete 'dist'
    delete 'build'
    delete 'log'
    delete 'temp'
}

jar {
    destinationDir file('dist/apps')
    archiveName project.name + '.jar'
    exclude '**/*.xml'
    exclude '**/*.properties'

    doLast {
        copy {
            from file('src/main/resources/')
            into 'dist/conf_template'
        }
        copy {
            from file('script/')
            into 'dist/script'
        }
        copy {
            from configurations.runtime
            into 'dist/lib'
        }
        copy {
            from file('.').listFiles().findAll{File f -> (f.name.endsWith('.sh') || f.name.endsWith('.env'))}
            into 'dist'
        }
    }
}
